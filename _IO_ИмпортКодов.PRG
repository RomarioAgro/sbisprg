Функция _IO_ИмпортКодов(пФайл)
{
#   Сообщить(пФайл);
   оФайл=Объект(пПуть+пФайл);
   оШтрих=Выборка("Штрих-коды");
   Пока(Следующий(оФайл)) Если(оФайл.Строка!="")
   {
      Объект оКоды; 
      Очистить(оКоды);
      Разбить(оФайл.Строка,",",оКоды);
      #оКоды.1 код товара Арктикул+Цвет+Рамер
      #оКоды.2 Штрих-код
      ВывестиСтатус("Проверка/создание штрих-кода "+_УбратьДвКв(оКоды.1)+"|"+_УбратьДвКв(оКоды.2),1);
      пКоды1=оКоды.1;
      оКоды.1=_УбратьДвКв(оКоды.1);
      оКоды.1=ПодСтрока("0000000000",1,Макс(10-Размер(оКоды.1),0))+оКоды.1;
#если нолик впереди надо ставить 25 и нолик убирать
#если не нолик то 2
      Если(Подстрока(оКоды.1,1,1)=="0") оКоды.1="25"+Подстрока(оКоды.1,2);
      Иначе оКоды.1="2"+оКоды.1
#      Сообщить(оКоды.1);Отладить();
      оКоды.1=оКоды.1+_КодКС_ЕАН13(оКоды.1);
      Очистить(оНом);
      оНом.НомНомер=оКоды.1;
      Найти(оНом,"НомНомер");
      пКЭм=0; 
      ДляВсех(Элементов(оКоды,эмКод)) пКЭм++;
      фПровКод=1;

      Если(оНом.НомНомер!=оКоды.1) фПровКод=3;
      Если(пКЭм!=2) фПровКод=2;
      Если(фПровКод==1)
      {
         пКод=_УбратьДвКв(оКоды.2);
         Если(_ПроверитьНН(пКод))
         {
            Очистить(оШтрих);
            оШтрих.Код=пКод;
            Найти(оШтрих,"Код");
            Если(оШтрих.Код!=пКод)
            {
               Очистить(оШтрих);
               оШтрих.Код=пКод;
               Связать('оШтрих.Номенклатура-Коды',оНом);
               Добавить(оШтрих);
            }
            Иначе
            {
               Связать('оШтрих.Номенклатура-Коды',оНом);
               Сохранить(оШтрих);
            }
         }
         
      }
      Если(фПровКод==2 ИЛИ фПровКод==3)
      {
         оЛог.Строка ="Штрих-код:"
         оЛог.Строка+=фПровКод==2?"Неверный формат строки":"Не найден товар с кодом "+пКоды1;
         Добавить(оЛог); 
         Очистить(оЛог);
         оЛог.Строка="  "+оФайл.Строка;
         Добавить(оЛог); 
         Очистить(оЛог);
      }
   }
   Очистить(оФайл);
   Вернуть 1;
}
