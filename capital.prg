# Функции для учета основных средств

Функция ГруппаАмортизации( пСрок )
#  Возвращает группу амортизации в зависимости от срока полезного использования.
#  Используется для автоматической инициализации поля "Группа амортизации" при приходе ОС
{
   перем пГруппа = 0;
   Если( пСрок >=13  и пСрок <= 24) пГруппа = 1  Иначе
   Если( пСрок >=25  и пСрок <= 36) пГруппа = 2  Иначе
   Если( пСрок >=37  и пСрок <= 60) пГруппа = 3  Иначе
   Если( пСрок >=61  и пСрок <= 84) пГруппа = 4  Иначе
   Если( пСрок >=85  и пСрок <=120) пГруппа = 5  Иначе
   Если( пСрок >=121 и пСрок <=180) пГруппа = 6  Иначе
   Если( пСрок >=181 и пСрок <=240) пГруппа = 7  Иначе
   Если( пСрок >=241 и пСрок <=300) пГруппа = 8  Иначе
   Если( пСрок >=301 и пСрок <=360) пГруппа = 9  Иначе
   Если( пСрок >=361              ) пГруппа = 10;
   Вернуть пГруппа;
}

Функция ВводВЭксплуатацию()
# Не рекомендуется к использованию, оставлена только для совместимости
# Возвращает дату ввода основного средства в эксплуатацию.
# Для средств, не введенных в эксплуатацию возвращает пустую дату (00.00.00).
{
   Вернуть ВЭксплуатации();
}

Функция СрокНачисленнойАмортизацииН( Дата )
# Возвращает количество месяцев, за которые начислялась амортизация по основному средству
# до указанной даты включительно
# используется в переходной ведомости амортизации
{
   Перем пВводВЭкспл = ВЭксплуатации();
   Вернуть Макс( 0, - 1 + Месяц(Дата) - Месяц(пВводВЭкспл) + 12 * ( Год(Дата) - Год(пВводВЭкспл) ) );
}

Функция АмортизацияНПредМес()
# Возвращает сумму амортизации, начисленной в предыдущем месяце.
# Используется для расчета налоговой амортизации нелинейным методом.
{
   Объект оЛицо = Лицо;
   Объект оВед  = Объект("Список ведомости");
   # Перебираем строки норм амортизации
   Пока( Предыдущий( оЛицо, оВед, "Лица-Список" ) )
   {  
      # Документ "Амортизация ОС", Суммируем строки с ненулевой налоговой амортизацией
      Если( 'оВед.Ведомость-Список>Документ_.Тип документа' == "АмортизацияОС"
            и оВед.НОстаток > 0 )
         Вернуть оВед.НОстаток;
   }
   Вернуть 0;
}

Функция ГруппаАмортизацииУпрощенка( пСрок )
#  Возвращает группу амортизации ОС, приобретенных по перехода на УСН,
#  в зависимости от срока полезного использования.
#  Используется для автоматической инициализации поля "Группа амортизации" при приходе ОС
{
   перем пГруппа = 0;
   Если( пСрок <=36 )  пГруппа = 1  Иначе
   Если( пСрок <= 180) пГруппа = 2  Иначе пГруппа = 3;
   Если( ВЭксплуатации() >= Дата( ДАТА_УСН ) ) пГруппа = 4;
   Вернуть пГруппа;
}
